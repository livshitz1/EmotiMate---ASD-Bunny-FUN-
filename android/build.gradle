// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:9.0.0'
        classpath 'com.google.gms:google-services:4.4.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

ext.kotlinStdlibVersion = "1.8.22"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == "org.jetbrains.kotlin") {
                if (details.requested.name == "kotlin-stdlib-jdk7" || details.requested.name == "kotlin-stdlib-jdk8") {
                    details.useTarget "org.jetbrains.kotlin:kotlin-stdlib:${rootProject.ext.kotlinStdlibVersion}"
                } else if (details.requested.name.startsWith("kotlin-stdlib")) {
                    details.useVersion rootProject.ext.kotlinStdlibVersion
                }
                details.because("Avoid duplicate Kotlin stdlib classes from mixed versions")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
